buildscript {
    ext {
        shadowJarVersion = '5.1.0'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:${shadowJarVersion}"
    }
}

plugins {
    id "com.github.ben-manes.versions" version '0.29.0'
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'com.diffplug.spotless' version '5.2.0'
    id 'com.bmuschko.docker-java-application' version '5.2.0'
}

apply from: "$rootDir/gradle-scripts/extensions.gradle"
apply from: "$rootDir/gradle-scripts/plugins.gradle"
apply from: "$rootDir/gradle-scripts/project-info.gradle"
apply from: "$rootDir/gradle-scripts/java-compile.gradle"
apply from: "$rootDir/gradle-scripts/repositories.gradle"
apply from: "$rootDir/gradle-scripts/test.gradle"
apply from: "$rootDir/gradle-scripts/integration-tests.gradle"
apply from: "$rootDir/gradle-scripts/test-logger.gradle"


ext {
    commercetoolsJvmSdkVersion =  '1.52.0'
    mockitoVersion = '3.5.7'
    slf4jVersion = '1.7.30'
    slf4jTestVersion = '1.2.0'
    assertjVersion = '3.16.1'
    pmdVersion = '5.6.1'
    jacocoVersion = '0.7.9'
    findbugsVersion = '3.0.1'
    commercetoolsSyncJava='1.9.1'
    apacheCliVersion = '1.4'
    jupiterApiVersion = '5.6.2'
}



apply from: "$rootDir/gradle-scripts/spotless.gradle"
apply from: "$rootDir/gradle-scripts/pmd.gradle"
apply from: "$rootDir/gradle-scripts/jacoco.gradle"
apply from: "$rootDir/gradle-scripts/findbugs.gradle"
apply from: "$rootDir/gradle-scripts/shadow.gradle"
apply from: "$rootDir/gradle-scripts/docker.gradle"
apply from: "$rootDir/gradle-scripts/execution-order.gradle"

/**
 * 1. This cannot be in the "gradle-scripts/execution-order.gradle" script due to
 * "Script plugins do not have access to the main buildscript classpath.":
 * https://discuss.gradle.org/t/cant-access-classes-from-plugin-directly-in-included-script/14721/2
 *
 * 2. Cannot use normal dependsOn syntax due to https://github.com/diffplug/spotless/issues/198#issuecomment-363542192
 */

import com.diffplug.gradle.spotless.SpotlessTask
tasks.withType(SpotlessTask) {
    it.mustRunAfter compileIntegrationTestJava
    pmdMain.mustRunAfter it
}

dependencies {
    implementation "com.commercetools.sdk.jvm.core:commercetools-models:${commercetoolsJvmSdkVersion}"
    implementation "com.commercetools.sdk.jvm.core:commercetools-java-client:${commercetoolsJvmSdkVersion}"
    implementation "com.commercetools:commercetools-sync-java:${commercetoolsSyncJava}"
    implementation "com.commercetools.sdk.jvm.core:commercetools-convenience:${commercetoolsJvmSdkVersion}"
    implementation "commons-cli:commons-cli:${apacheCliVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "com.google.code.findbugs:annotations:${findbugsVersion}"
    testImplementation "uk.org.lidalia:slf4j-test:${slf4jTestVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterApiVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterApiVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterApiVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}
