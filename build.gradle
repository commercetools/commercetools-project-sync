buildscript {
    ext {
        shadowJarVersion = '5.1.0'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:${shadowJarVersion}"
    }
}

plugins {
    id "com.github.ben-manes.versions" version '0.38.0'
    id 'com.adarshr.test-logger' version '2.1.1'
    id 'com.diffplug.spotless' version '5.12.0'
    id 'com.bmuschko.docker-java-application' version '6.7.0'
}

apply from: "$rootDir/gradle-scripts/extensions.gradle"
apply from: "$rootDir/gradle-scripts/plugins.gradle"
apply from: "$rootDir/gradle-scripts/project-info.gradle"
apply from: "$rootDir/gradle-scripts/java-compile.gradle"
apply from: "$rootDir/gradle-scripts/repositories.gradle"
apply from: "$rootDir/gradle-scripts/test.gradle"
apply from: "$rootDir/gradle-scripts/integration-tests.gradle"
apply from: "$rootDir/gradle-scripts/test-logger.gradle"


ext {
    commercetoolsJvmSdkVersion =  '1.60.0'
    mockitoVersion = '3.8.0'
    slf4jVersion = '1.7.30'
    slf4jTestVersion = '1.2.0'
    assertjVersion = '3.19.0'
    pmdVersion = '5.6.1'
    jacocoVersion = '0.8.6'
    findbugsVersion = '3.0.1'
    commercetoolsSyncJava='5.1.0'
    apacheCliVersion = '1.4'
    jupiterApiVersion = '5.7.1'
    caffeineVersion = '2.8.5'
    asyncHttpClientVersion = '2.12.3'
    logbackVersion = '1.2.3'
    logstashLogbackEncoderVersion= '6.6'
}



apply from: "$rootDir/gradle-scripts/spotless.gradle"
apply from: "$rootDir/gradle-scripts/pmd.gradle"
apply from: "$rootDir/gradle-scripts/jacoco.gradle"
apply from: "$rootDir/gradle-scripts/findbugs.gradle"
apply from: "$rootDir/gradle-scripts/shadow.gradle"
apply from: "$rootDir/gradle-scripts/docker.gradle"
apply from: "$rootDir/gradle-scripts/execution-order.gradle"

/**
 * 1. This cannot be in the "gradle-scripts/execution-order.gradle" script due to
 * "Script plugins do not have access to the main buildscript classpath.":
 * https://discuss.gradle.org/t/cant-access-classes-from-plugin-directly-in-included-script/14721/2
 *
 * 2. Cannot use normal dependsOn syntax due to https://github.com/diffplug/spotless/issues/198#issuecomment-363542192
 */

import com.diffplug.gradle.spotless.SpotlessTask
tasks.withType(SpotlessTask) {
    it.mustRunAfter compileIntegrationTestJava
    pmdMain.mustRunAfter it
}

dependencies {
    implementation "com.commercetools:commercetools-sync-java:${commercetoolsSyncJava}"

    implementation "com.commercetools.sdk.jvm.core:commercetools-models:${commercetoolsJvmSdkVersion}"
    implementation "com.commercetools.sdk.jvm.core:commercetools-java-client-ahc-2_5:${commercetoolsJvmSdkVersion}"
    implementation "org.asynchttpclient:async-http-client:${asyncHttpClientVersion}"

    implementation "commons-cli:commons-cli:${apacheCliVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"

    implementation "com.google.code.findbugs:annotations:${findbugsVersion}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"
    constraints {
        implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.12.3") {
            because "previous versions are vulnerable to DoS"
        }
        implementation("io.netty:netty-codec-http:4.1.63.Final") {
            because "previous versions are vulnerable to Information Disclosure"
        }
    }
    testImplementation "uk.org.lidalia:slf4j-test:${slf4jTestVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterApiVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterApiVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterApiVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}
