buildscript {
    ext {
        shadowJarVersion = '8.1.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.github.johnrengelman:shadow:${shadowJarVersion}"
    }
}

plugins {
    id "com.github.ben-manes.versions" version '0.52.0'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'com.diffplug.spotless' version '7.2.1'
    id "com.github.spotbugs" version "6.2.6"
}

apply from: "$rootDir/gradle-scripts/extensions.gradle"
apply from: "$rootDir/gradle-scripts/plugins.gradle"
apply from: "$rootDir/gradle-scripts/project-info.gradle"
apply from: "$rootDir/gradle-scripts/java-compile.gradle"
apply from: "$rootDir/gradle-scripts/repositories.gradle"
apply from: "$rootDir/gradle-scripts/test.gradle"
apply from: "$rootDir/gradle-scripts/integration-tests.gradle"
apply from: "$rootDir/gradle-scripts/test-logger.gradle"
apply from: "$rootDir/gradle-scripts/set-library-version.gradle"


ext {
    mockitoVersion = '5.19.0'
    slf4jTestVersion = '3.0.3'
    assertjVersion = '3.27.4'
    pmdVersion = '7.16.0'
    jacocoVersion = '0.8.13'
    commercetoolsSyncJava = '10.2.0'
    httpClientVersion = '18.3.0'
    apacheCliVersion = '1.10.0'
    jupiterApiVersion = '5.12.2'
    logbackVersion = '1.5.18'
    logstashLogbackEncoderVersion= '8.1'
    jupiterPlatformVersion = '1.12.2'
}

apply from: "$rootDir/gradle-scripts/spotless.gradle"
apply from: "$rootDir/gradle-scripts/pmd.gradle"
apply from: "$rootDir/gradle-scripts/jacoco.gradle"
apply from: "$rootDir/gradle-scripts/spotbugs.gradle"
apply from: "$rootDir/gradle-scripts/shadow.gradle"
apply from: "$rootDir/gradle-scripts/execution-order.gradle"

/**
 * 1. This cannot be in the "gradle-scripts/execution-order.gradle" script due to
 * "Script plugins do not have access to the main buildscript classpath.":
 * https://discuss.gradle.org/t/cant-access-classes-from-plugin-directly-in-included-script/14721/2
 *
 * 2. Cannot use normal dependsOn syntax due to https://github.com/diffplug/spotless/issues/198#issuecomment-363542192
 */

import com.diffplug.gradle.spotless.SpotlessTask
tasks.withType(SpotlessTask) {
    it.mustRunAfter compileIntegrationTestJava
    pmdMain.mustRunAfter it
}

dependencies {
    implementation "com.commercetools:commercetools-sync-java:${commercetoolsSyncJava}"
    implementation "com.commercetools.sdk:commercetools-okhttp-client4:${httpClientVersion}"
    implementation "commons-cli:commons-cli:${apacheCliVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"
    testImplementation "com.github.valfirst:slf4j-test:${slf4jTestVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterApiVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterApiVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterApiVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${jupiterPlatformVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}
